plugins {
    id 'java'
    id 'idea'
    id 'checkstyle'
    id "com.google.protobuf" version "0.8.10"
}

group 'my.grpc.service'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.11.1'
    implementation 'io.grpc:grpc-netty-shaded:1.25.0'
    implementation 'io.grpc:grpc-protobuf:1.25.0'
    implementation 'io.grpc:grpc-stub:1.25.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

sourceSets {
    main {
        proto {
            // In addition to the default 'src/main/proto'
            srcDirs 'src/main/proto'
        }
        java {
            srcDirs 'src/main/java', 'generated/main/java', 'generated/main/grpc'
        }
    }
    test {
        java {
            srcDirs 'src/test/java'
        }
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.11.1'
    }

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.25.0'
        }
    }

    generateProtoTasks.generatedFilesBaseDir = 'generated'

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}
